apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.org.cesar.discordtime.stickysessions"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main { res.srcDirs = ['src/main/res'] }
        test { res.srcDirs = ['src/test/res/'] }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "server"

    productFlavors {
        online {
            dimension "server"
            ext {
                url = "http://142.93.124.175"
            }
        }
        local {
            dimension "server"
            applicationId = applicationId + ".local"
            ext {
                url = "http://localhost:3000"
            }
        }
        applicationVariants.all { variant ->
            def flavors = variant.productFlavors
            // flavorDimensions "server" -> 0
            def server = flavors[0]
            variant.buildConfigField "String", "URL", "\"${server[url.name]}\""
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
        abortOnError true
        checkAllWarnings true
        warningsAsErrors true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android X
    implementation "androidx.appcompat:appcompat:${versions.androidx}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx}"
    implementation "androidx.annotation:annotation:${versions.androidx}"
    implementation "androidx.legacy:legacy-support-v4:${versions.androidx}"
    implementation "androidx.cardview:cardview:${versions.androidx}"
    implementation "com.google.android.material:material:${versions.material}"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    // Rx
    implementation "io.reactivex.rxjava2:rxkotlin:${versions.rx_kotlin}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rx_android}"
    implementation "io.reactivex.rxjava2:rxjava:${versions.rx_java}"

    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"

    // OkHttp3
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"

    // Dagger
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'
    implementation 'com.google.dagger:dagger:2.17'

    // Unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation "com.nhaarman:mockito-kotlin:${versions.mockito_kotlin}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"

    // Instrumentation tests
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    androidTestImplementation 'com.google.dagger:dagger:2.17'

}
repositories {
    mavenCentral()
}
